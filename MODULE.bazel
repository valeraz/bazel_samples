module(name = "slack-android-ng")

include("//:maven.MODULE.bazel")

bazel_dep(name = "rules_java", version = "7.7.0")
bazel_dep(name = "rules_jvm_external", version = "6.2")
bazel_dep(name = "bazel_skylib", version = "1.7.1")
bazel_dep(name = "buildifier_prebuilt", version = "6.4.0")
bazel_dep(name = "apple_support", version = "1.15.1")
bazel_dep(name = "rules_proto", version = "6.0.0")

# rules_kotlin should be overridden below (use 0.0.0 as a safety net)
bazel_dep(name = "rules_kotlin", version = "0.0.0")

# rules_android should be overridden below (use 0.0.0 as a safety net)
bazel_dep(name = "rules_android", version = "0.0.0")

RULES_KOTLIN_SHA = "661e13ac3bc7c7195408bf22855b637d3379ade0"

archive_override(
    module_name = "rules_kotlin",
    integrity = "sha256-bJ2to0441Y7hcOuKXxL9X2W4qbrDfe4mQAVBzPXRxmk=",
    patches = [
        "//bazel/patches:0001-Re-add-kapt-processing.patch",
        "//bazel/patches:0002-Shade-Guava.patch",
        "//bazel/patches:0003-Various-kotlinc-options-added-to-opts.kotlinc.bzl.patch",
    ],
    strip_prefix = "rules_kotlin-{}".format(RULES_KOTLIN_SHA),
    urls = ["https://github.com/Bencodes/rules_kotlin/archive/{}.zip".format(RULES_KOTLIN_SHA)],
)

rules_kotlin_extensions = use_extension(
    "@rules_kotlin//src/main/starlark/core/repositories:bzlmod_setup.bzl",
    "rules_kotlin_extensions",
)
rules_kotlin_extensions.kotlinc_version(
    sha256 = "93137d3aab9afa9b27cb06a824c2324195c6b6f6179d8a8653f440f5bd58be88",
    version = "1.9.23",
)
rules_kotlin_extensions.ksp_version(
    sha256 = "423b8841cae9296aacfb529a29bbe6a911454a26fcdd03b25fecc79627a69512",
    version = "1.9.23-1.0.19",
)
use_repo(rules_kotlin_extensions)

RULES_ANDROID_SHA = "5eb812227658d3a2691dae731a9c07e9d41c6323"

archive_override(
    module_name = "rules_android",
    integrity = "sha256-Lx9Sk7K05K2BknkOhPJTCb2ogrCEWyALjTZZuL55Eyo=",
    strip_prefix = "rules_android-{}".format(RULES_ANDROID_SHA),
    urls = ["https://github.com/bencodes/rules_android/archive/{}.zip".format(RULES_ANDROID_SHA)],
)

android_sdk_repository_extension = use_extension("@rules_android//rules/android_sdk_repository:rule.bzl", "android_sdk_repository_extension")
use_repo(android_sdk_repository_extension, "androidsdk")

register_toolchains(
    "@androidsdk//:sdk-toolchain",
    "@androidsdk//:all",
)

remote_android_extensions = use_extension("@bazel_tools//tools/android:android_extensions.bzl", "remote_android_tools_extensions")
use_repo(remote_android_extensions, "android_gmaven_r8")

register_toolchains(
    # Providing a custom Android toolchain so that we can wrap tools
    "//:slack_android_default_toolchain",
    "//:kotlin_toolchain",
)

apple_cc_configure = use_extension("@apple_support//crosstool:setup.bzl", "apple_cc_configure_extension")
use_repo(apple_cc_configure, "local_config_apple_cc")
